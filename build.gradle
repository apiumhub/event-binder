// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = "1.3.50"

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.5.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "maven-publish"
    id "digital.wup.android-maven-publish" version "3.6.2"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext.coroutinesVersion = "1.3.0"
ext.androidLifecycle = "2.2.0-alpha03"

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "digital.wup.android-maven-publish"

    project.afterEvaluate {
        if (!project.plugins.hasPlugin("android")) {
            publishing {
                publications {
                    maven(MavenPublication) {
                        groupId = "dev.martori"
                        artifactId = "$rootProject.name-$project.name"
                        version = "1.0.0"

                        if (project.plugins.hasPlugin("java")) {
                            from components.java
                        } else if (project.plugins.hasPlugin("android-library")) {
                            from components.android
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
