// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.50"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "digital.wup.android-maven-publish" version "3.6.2" apply false
}

allprojects {
    group = "dev.martori"
    version = "1.0.0"

    repositories {
        google()
        jcenter()
    }
}

ext.coroutinesVersion = "1.3.0"
ext.androidLifecycle = '2.2.0-rc02'

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "digital.wup.android-maven-publish"

    afterEvaluate {
        def isJava = plugins.hasPlugin("java")
        def isAndroid = plugins.hasPlugin("android-library")
        def shouldPublish = !plugins.hasPlugin("android")

        if (isJava) {
            task jvmSourcesJar(type: Jar) {
                archiveClassifier.set("sources")
                from sourceSets.main.allSource
            }
        }
        if (isAndroid) {
            task androidSourcesJar(type: Jar) {
                archiveClassifier.set("sources")
                from android.sourceSets.main.java.srcDirs
            }
        }
        if (shouldPublish) {
            publishing {
                publications {
                    maven(MavenPublication) {
                        artifactId = "$rootProject.name-$project.name"
                        if (isJava) {
                            from components.java
                            artifact jvmSourcesJar
                        } else if (isAndroid) {
                            from components.android
                            artifact androidSourcesJar
                        }
                    }
                }
                repositories {
                    maven {
                        name 'NexusSNAPSHOT'
                        url snapshotsRepositoryUrl
                        credentials {
                            username = deployRepoUsername
                            password = deployRepoPassword
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}